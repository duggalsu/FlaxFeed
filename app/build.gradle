apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
// TODO: migrate
// apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.devtools.ksp'

// TODO: migrate
/**
androidExtensions {
    experimental = true
}
 **/

android {
    compileSdk 30
    defaultConfig {
        applicationId "net.frju.flym"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 40
        versionName "2.5.6"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true"
                ]
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8

        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    buildFeatures {
        viewBinding = true
    }

    File localProps = project.rootProject.file('local.properties')
    Properties properties = new Properties()
    if (localProps.exists()) {
        properties.load(localProps.newDataInputStream())
    }

    signingConfigs {
        debug {
            if (properties.getProperty("KEYSTORE_PATH") != null) {
                storeFile file(properties.getProperty("KEYSTORE_PATH"))
                storePassword properties.getProperty("KEYSTORE_PASSWORD")
                keyAlias properties.getProperty("KEY_ALIAS")
                keyPassword properties.getProperty("KEY_PASSWORD")
            }
        }
        release {
            if (properties.getProperty("KEYSTORE_PATH") != null) {
                storeFile file(properties.getProperty("KEYSTORE_PATH"))
                storePassword properties.getProperty("KEYSTORE_PASSWORD")
                keyAlias properties.getProperty("KEY_ALIAS")
                keyPassword properties.getProperty("KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (properties.getProperty("KEYSTORE_PATH") != null) {
                signingConfig signingConfigs.release
            }
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    namespace 'net.fred.feedex'
    lint {
        abortOnError true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    def room_version = "2.5.2"
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'

    def okhttp_version = '4.11.0'
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okhttp_version"

    def glide_version = '4.15.1'
    implementation "com.github.bumptech.glide:glide:$glide_version"
    ksp "com.github.bumptech.glide:compiler:$glide_version"

    implementation 'androidx.core:core-ktx:1.12.0-rc01'
    implementation 'androidx.paging:paging-runtime-ktx:3.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'com.github.ramseth001:TextDrawable:1.1.6'
    implementation 'com.github.Tunous:SwipeActionView:1.3.1'
    implementation 'com.bignerdranch.android:expandablerecyclerview:3.0.0-RC1'
    implementation 'com.github.jrvansuita:MaterialAbout:0.2.3'
    implementation 'com.github.kimoandroid:Smart-BadgeView:1.0.9'
    implementation 'net.dankito.readability4j:readability4j:1.0.5'
    implementation 'pub.devrel:easypermissions:3.0.0'
    implementation 'com.rometools:rome-opml:1.15.0'

    testImplementation 'junit:junit:4.13.1'
}
